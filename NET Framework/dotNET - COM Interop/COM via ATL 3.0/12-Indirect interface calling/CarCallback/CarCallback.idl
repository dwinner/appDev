/*

This IDL file describes all the COM-isms
contained in the CarCallBack Library

*/

// Bring in core IDL files.
import "oaidl.idl";	

// IStats
[object, uuid(A533DA31-D372-11d2-B8CF-0020781238D4),
 helpstring("Get info about this car")]
interface IStats : IUnknown
{
	HRESULT DisplayStats();
	HRESULT GetPetName([out, retval] BSTR* petName);
};

// IEngine
[object, uuid(A533DA30-D372-11d2-B8CF-0020781238D4),
 helpstring("Rev your car & slow it down")]
interface IEngine : IUnknown
{
	HRESULT SpeedUp();
	HRESULT GetMaxSpeed ([out, retval] int* maxSpeed);
	HRESULT GetCurSpeed ([out, retval] int* curSpeed);
};


// ICreateCar
[object, uuid(A533DA32-D372-11d2-B8CF-0020781238D4),
 helpstring("This lets you create a car")]
interface ICreateCar : IUnknown
{
	HRESULT SetPetName([in]BSTR petName);
	HRESULT SetMaxSpeed([in]int maxSp);
};


// IEngineEvents is implemented by the CLIENT.
[uuid(893D8210-688A-11d3-B929-0020781238D4), object]
interface IEngineEvents : IUnknown
{
	HRESULT AboutToExplode();
	HRESULT Exploded();
};

// IEstablishCommunications
// Used to allow a client to send us a valid sink.
//
[uuid(CB26A7F0-688B-11d3-B929-0020781238D4), object]
interface IEstablishCommunications : IUnknown
{
	HRESULT Advise([in]IEngineEvents *pCallMe);
	HRESULT Unadvise();
};


[uuid(7005A5E0-688C-11d3-B929-0020781238D4), version(1.0),
 helpstring("CarCallBack server")]
library CarCallBackServer
{
	importlib("stdole32.tlb");
	
	// Need to forward declare to get into typelib.
	// Not implemented by coclass!!!!
	interface IEngineEvents;


	[uuid(881552B0-688C-11d3-B929-0020781238D4)]
	coclass CoCarCallBack
	{
		[default] interface ICreateCar;
		interface IEstablishCommunications;
		interface IStats;
		interface IEngine;
	};
};
